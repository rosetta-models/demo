namespace demo.functions
version "${project.version}"

import demo.types.*
import demo.enumeration.*

func Create_VehicleOwnership: <"Creation of a vehicle ownership record file">
    inputs: 
        drivingLicence DrivingLicence (0..*)
        vehicle Vehicle (1..1)
        dateOfPurchase date (1..1)
        isFirstHand boolean (1..1)
    output: 
        vehicleOwnership VehicleOwnership (1..1)

    condition: <"Driving licence must not be expired">
        drivingLicence -> dateOfRenewal all > dateOfPurchase

    condition: <"Vehicle classification allowed by the driving licence needs to encompass the vehicle classification of the considered vehicle">
        drivingLicence->vehicleEntitlement contains vehicle-> vehicleClassification

    set vehicleOwnership -> drivingLicence: 
        drivingLicence

    set vehicleOwnership -> vehicle: 
        vehicle

    set vehicleOwnership -> dateOfPurchase: 
        dateOfPurchase
 
    set vehicleOwnership -> isFirstHand: 
        isFirstHand

func Check_DrivingLicenceRenewalDate: <"Function to check whether the driving licence is expired or not">
    inputs: 
        drivingLicence DrivingLicence (1..1)
        todayDate date (1..1)
    output:
        isExpired boolean (1..1)

    set isExpired: 
        if drivingLicence->dateOfRenewal < todayDate then True
        else False

 func Check_DieselEngine: <"Function to check whether the engine of the vehicle is of diesel type">
    inputs: 
        engine Engine (1..1)
    output:
        isEngineTypeDiesel boolean (1..1)

    set isEngineTypeDiesel: 
        if engine-> engineType = EngineTypeEnum -> Diesel then True 
        else False

func Check_YearOfManufacture: <"Function to check whether the year of manufacture of the vehicle is older than a year cut">
    inputs: 
        specification Specification (1..1)
        yearCut int (1..1)
    output:
        isOlderThanYearCut boolean (1..1)
    
    set isOlderThanYearCut: 
        if specification-> yearOfManufacture < yearCut then True 
        else False

func Create_Date: <"Function to create a date for the given year, month and dayOfMonth.">
    inputs:
        year int (1..1)
        month int (1..1)
        day int (1..1)
    output:
        result date (1..1)

func FindVehiclesByEngineType: <"Find all vehicles with given engine type.">
    inputs:
        vehicles Vehicle (0..*)
        engineType EngineTypeEnum (1..1)
    output:
        vehiclesWithEngineType Vehicle (0..*)

    add vehiclesWithEngineType: <"Filter each list 'item' with the given test.">
        vehicles
            filter [ item -> specification -> engine -> engineType = engineType ]

 func FindDriversWithMaximumZeroTo60: <"Find all vehicles with given maximum 0 - 60 mph.">
    inputs:
        vehicles Vehicle (0..*)
        zeroTo60 number (1..1)
    output:
        vehiclesWithMaximumZeroTo60 Vehicle (0..*)

    add vehiclesWithMaximumZeroTo60: <"Each list 'item' can have a name specified.">
        vehicles
            filter vehicle [ vehicle -> specification -> zeroTo60 < zeroTo60 ]

func FindVehiclesWithinEmissionLimits: <"Find all vehicles within given emissions metrics.">
    inputs:
        vehicles Vehicle (0..*)
        coLimit int (0..1)
        noxLimit int (0..1)
        pmLimit int (0..1)
    output:
        vehiclesWithinEmissionLimits Vehicle (0..*)

    add vehiclesWithinEmissionLimits: <"Filter (and other list operations) can contain expressions, and can be chained together.">
        vehicles
            filter [ if coLimit exists then item -> specification -> engine -> emissionMetrics -> carbonMonoxide <= coLimit else True ]
            filter [ if noxLimit exists then item -> specification -> engine -> emissionMetrics -> nitrogenOxide <= noxLimit else True ]
            filter [ if pmLimit exists then item -> specification -> engine -> emissionMetrics -> particulateMatter <= pmLimit else True ]

func FindOwnersWithinPenaltyPointLimit: <"Find all owners within penalty point limits on any driver licence (owners can have multiple licences, issued by different countries).">
    inputs:
       owners VehicleOwnership (0..*)
       maximumPenaltyPoints int (1..1)
    output:
       ownersWithinPenaltyPointLimit VehicleOwnership (0..*)  

    add ownersWithinPenaltyPointLimit: <"Nested filter required to determine if all owner's licences do not exceed the penalty point limit.">
        owners
            filter owner [ owner -> drivingLicence
                filter licence [ licence -> penaltyPoints > maximumPenaltyPoints ] count = 0 
            ]

func GetDrivingLicenceNames: <"Get driver's names from given list of licences.">
    inputs:
        drivingLicences DrivingLicence (0..*)
    output:
        ownersName string (0..*)

    add ownersName: <"Filter lists to only include drivers with first and last names, then use 'map' to convert driving licences into list of names.">
        drivingLicences
            filter [ item -> firstName exists and item -> surname exists ]
            map [ item -> firstName + " " + item -> surname ]

func GetAllDrivingLicencesFromInternationalOwners: <"Get list of driving licences from owners with 2 or more licences.">
    inputs:
        owners VehicleOwnership (0..*) <"List of owners, each contain a list of driving licences.">
    output:
        drivingLicences DrivingLicence (0..*) <"Flattened list of driving licences.">

    add drivingLicences:
        owners
            // map each owner item into a list of driving licences
            map [ item -> drivingLicence ] // each item is now a list of driving licences
            // filter to include only items which contain more than one driving licence
            filter [ item count > 1 ] // each item is still a list of driving licences
            // flatten list so output is a "flat" list of driving licences (rather that a list where each item is a list of driving licences)
            flatten

func GetDrivingLicence: <"Find driving licence for the given licence number.">
    inputs:
        drivingLicences DrivingLicence (0..*)
        licenceNumber string (1..1)
    output:
        drivingLicence DrivingLicence (0..1)

    set drivingLicence:
        drivingLicences
            filter [ item -> licenceNumber = licenceNumber ] 
            only-element

func AddDrivingLicenceToVehicleOwnership: <"Add new driving licence to vehicle owner.">
    inputs:
        vehicleOwnership VehicleOwnership (1..1)
        newDrivingLicence DrivingLicence (1..1)
    output:
        updatedVehicleOwnership VehicleOwnership (1..1)

    set updatedVehicleOwnership: 
        vehicleOwnership
    
    add updatedVehicleOwnership -> drivingLicence: <"Add newDrivingLicence to existing list of driving licences">
        newDrivingLicence

func RenewAllDrivingLicences: <"Renew all vehicle owner's driving licences.">
    inputs:
        vehicleOwnership VehicleOwnership (1..1)
        newDateOfRenewal date (1..1)
    output:
        updatedVehicleOwnership VehicleOwnership (1..1)

    set updatedVehicleOwnership: 
        vehicleOwnership
    
    set updatedVehicleOwnership -> drivingLicence: <"Overwrite existing driving licences with renewed driving licences.">
        vehicleOwnership -> drivingLicence
            map [ RenewDrivingLicence( item, newDateOfRenewal ) ]

func RenewDrivingLicence: <"Create new driving licence with new date of renewal.">
    inputs:
        drivingLicence DrivingLicence (1..1)
        newDateOfRenewal date (1..1)
    output:
        renewedDrivingLicence DrivingLicence (1..1)
    
    set renewedDrivingLicence: 
        drivingLicence

    set renewedDrivingLicence -> dateOfRenewal:
        newDateOfRenewal

func SumNumbers: <"Returns the sum of the given list of numbers.">
    inputs:
        numbers number (0..*)
    output:
        total number (1..1)

    set total:
        numbers sum

func JoinStrings: <"Concatenates the list of strings, separating each element with the given delimiter, using the join keyword.">
    inputs:
        strings string (0..*)
    output:
        result string (1..1)

    set result:
        strings join [ ", " ]

func FindMaxNumber: <"Returns the highest number from the given list of numbers, using the max keyword.">
    inputs:
        numbers number (0..*)
    output:
        result number (1..1)

    set result:
        numbers max

func SortDatesChronologically: <"Sorts the list of dates chronologically.">
    inputs:
        dates date (0..*)
    output:
        sortedDates date (0..*)

    set sortedDates:
        dates sort

func GetFirst: <"Get the first date.">
    inputs:
        dates date (0..*)
    output:
        firstDate date (0..1)

    set firstDate:
        dates first

func FindVehicleWithMaxPower: <"Returns the vehicle with the highest power engine.">
    inputs:
        vehicles Vehicle (0..*)
    output:
        vehicleWithMaxPower Vehicle (1..1)

    set vehicleWithMaxPower:
        vehicles
            max [ item -> specification -> engine -> power ]

func FindVehicleWithMaxPowerUsingReduce: <"Returns the vehicle with the highest power engine, using the reduce keyword.">
    inputs:
        vehicles Vehicle (0..*)
    output:
        vehicleWithMaxPower Vehicle (1..1)

    set vehicleWithMaxPower:
        vehicles
            reduce v1, v2 [
                if v1 -> specification -> engine -> power > v2 -> specification -> engine -> power then
                    v1
                else
                    v2
            ]
